/*
 * Copyright (c) 2017. Alfanse Developers
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except
 *  in compliance with the License. You may obtain a copy of the License at
 *  http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software distributed under the License
 * is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the License for the specific language governing permissions
 * and limitations under the License.
 */

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    google()
}


android {

    def isRunningOnTravis = System.getenv("CI") == "true"

    def PRODUCTION_PROPS_FILE = PRODUCTION_PROPS_FILE
    def RELEASE_KEYSTORE_PROPS_FILE = RELEASE_KEYSTORE_PROPS_FILE

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        config {
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file(RELEASE_KEYSTORE_PROPS_FILE)))
            keyAlias keyProps["RELEASE_KEY_ALIAS"]
            keyPassword keyProps["RELEASE_KEY_PASSWORD"]
            storeFile file(keyProps["RELEASE_STORE_FILE"])
            storePassword keyProps["RELEASE_STORE_PASSWORD"]
        }
    }
    compileSdkVersion 27
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.alfanse.author"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 15
        versionName "3.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            proguardFile 'proguard-rules.pro'
            matchingFallbacks = ['debug']
        }
    }

    if (isRunningOnTravis) {
        // configure keystore
        signingConfigs.config.storeFile = file("../signin_keys/Author.jks")
        signingConfigs.config.storePassword = System.getenv("keyPassword")
        signingConfigs.config.keyAlias = System.getenv("keyAlias")
        signingConfigs.config.keyPassword = System.getenv("keyPassword")
    }
    dexOptions {
        jumboMode = true
    }

    flavorDimensions "default"
    productFlavors {
        dev {
            dimension "default"
            def Properties devProps = new Properties()
            devProps.load(new FileInputStream(file(DEVELOPMENT_PROPS_FILE)))
            applicationIdSuffix devProps["APPLICATION_ID_SUFFIX"]
            versionNameSuffix devProps["VERSION_NAME_SUFFIX"]
            buildConfigField("String", "API_BASE_URL", devProps["API_BASE_URL"])
            buildConfigField("String", "WEB_BASE_URL", devProps["WEB_BASE_URL"])
            buildConfigField("String", "API_TOKEN", devProps["API_TOKEN"]);
            resValue("string", "app_name", devProps["APP_NAME"])
        }
        prod {
            dimension "default"
            def Properties prodProps = new Properties()
            prodProps.load(new FileInputStream(file(PRODUCTION_PROPS_FILE)))
            buildConfigField("String", "API_BASE_URL", prodProps["API_BASE_URL"])
            buildConfigField("String", "WEB_BASE_URL", prodProps["WEB_BASE_URL"])
            buildConfigField("String", "API_TOKEN", prodProps["API_TOKEN"]);
            resValue("string", "app_name", prodProps["APP_NAME"])
        }
    }
}

dependencies {
    implementation 'com.google.firebase:firebase-messaging:17.3.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation('com.twitter.sdk.android:twitter:2.3.2@aar') {
        transitive = true;
    }
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.jakewharton:butterknife:8.5.1'
    implementation 'com.google.firebase:firebase-database:16.0.2'
    implementation 'com.github.bumptech.glide:glide:4.0.0'
    implementation 'com.jrummyapps:colorpicker:2.1.6'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.4.+'
    implementation 'com.android.support:palette-v7:27.0.2'
    implementation 'com.facebook.android:facebook-android-sdk:[4,5)'
    implementation 'com.google.firebase:firebase-auth:16.0.3'
    implementation 'com.google.android.gms:play-services-auth:16.0.0'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.toptoche.searchablespinner:searchablespinnerlibrary:1.3.1'
    implementation 'com.github.GrenderG:Toasty:1.2.5'
    implementation 'com.kila.apprater_dialog.lars:apprater-dialog:1.2.0'
    testImplementation 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
    implementation 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.3.0'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.github.irshulx:laser-native-editor:3.0.3'
}
apply plugin: 'com.google.gms.google-services'
